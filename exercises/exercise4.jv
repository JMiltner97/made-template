pipeline DataPipeline {

    //  define pipeline
    EntryStep
        -> ZipArchiveInterpreter
        -> SelectDataFile
        -> TextFileInterpreter
        -> DataCSVInterpreter
        -> TempRename
        -> BatRename
        -> TableTransformer
        -> CToFTransformerTemp
        -> CToFTransformerBatterie
        -> OutputLoader
        ;

    //define datasource
    block EntryStep oftype HttpExtractor {
        url: "https://www.mowesta.com/data/measure/mowesta-dataset-20221107.zip";
    }

    block ZipArchiveInterpreter oftype ArchiveInterpreter {
        archiveType: "zip";
    }

    block SelectDataFile oftype FilePicker {
        path: "/data.csv";
    }

    block TextFileInterpreter oftype TextFileInterpreter { 

    }

    block DataCSVInterpreter  oftype CSVInterpreter {  
    delimiter: ";";
    }

    block TempRename oftype CellWriter{
        at: cell E1;
        write: ['Temperatur'];
    }

    block BatRename oftype CellWriter{
        at: cell M1;
        write: ['Batterietemperatur'];
    }

    block TableTransformer oftype TableInterpreter {
        header: true;
         columns: [
            "Geraet" oftype deviceId,
            "Hersteller" oftype text,
            "Model" oftype text,
            "Monat" oftype integer,
            "Temperatur" oftype temperaturTypeC,
            "Batterietemperatur" oftype temperaturTypeC,
            "Geraet aktiv" oftype geraetStatus
        ];
    }

    transform celsiusToFahrenheit{
        from cel oftype temperaturTypeC;
        to fahr oftype decimal;

        fahr: (cel *9/5)+32;
    }

    block CToFTransformerTemp oftype TableTransformer{
        inputColumns: ["Temperatur"];
        outputColumn: "Temperatur";
        use: celsiusToFahrenheit;
    }

    block CToFTransformerBatterie oftype TableTransformer{
        inputColumns: ["Batterietemperatur"];
        outputColumn: "Batterietemperatur";
        use: celsiusToFahrenheit;
    }

    block OutputLoader oftype SQLiteLoader {
        table: "temperatures";
        file: "./temperatures.sqlite";
    } 

    //Only valid deviceIds
    valuetype deviceId oftype integer {
        constraints: [ deviceIdConstraint ];
    }

    //Only valid deviceIds 
    constraint deviceIdConstraint oftype RangeConstraint {
      lowerBound: 1;
    }

    //Only valid states
    valuetype geraetStatus oftype text {
        constraints: [ geraetStatusConstraint ];
    }

    //Only valid deviceIds 
    constraint geraetStatusConstraint oftype AllowlistConstraint {
      allowlist: ['Ja', 'Nein'];
    }

    //Only valid months
    valuetype monatType oftype integer {
        constraints: [ monatConstraint ];
    }

    //Only valid months 
    constraint monatConstraint oftype RangeConstraint {
      lowerBound: 1;
      upperBound: 12;
    }

    //Only allow values >= −273,15 (abs. zero)
    valuetype temperaturTypeC oftype decimal {
        constraints: [ temperatureConstraintC ];
    }

    //Only allow values >= −273,15 (abs. zero)
    constraint temperatureConstraintC on decimal:
        value >= -273.15;
}